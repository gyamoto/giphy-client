apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.gyamoto.giphy"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets.all {
        java.srcDirs += "src/${name}/kotlin"
    }

    flavorDimensions 'source'

    productFlavors {
        mock {
            dimension 'source'
            applicationIdSuffix '.mock'
            versionNameSuffix '.mock'
        }
        develop {
            dimension 'source'
            applicationIdSuffix '.dev'
            versionNameSuffix ' dev'
        }
        product {
            dimension 'source'
        }
    }

    repositories {
        maven { url "https://jitpack.io" }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx_version"

    // SupportLibrary
    implementation "com.android.support:support-compat:$support_library_version"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    kapt 'com.android.databinding:compiler:3.0.1'

    // Kodein
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'com.github.salomonbrys.kodein:kodein-android:4.1.0'

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation "io.reactivex.rxjava2:rxjava:$rx_version"

    // Redux
    implementation 'com.github.pardom:redux-kotlin:1.2.1'

    // Architecture Components, ViewModel and Resource
    implementation "android.arch.lifecycle:extensions:$arch_version"
    kapt "android.arch.lifecycle:compiler:$arch_version"

    // Giphy
    implementation project(':api')
    // PersistentRandom
    implementation project(':db')

    // View
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    kapt 'com.github.bumptech.glide:compiler:4.5.0'

    // Logger
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testImplementation 'org.robolectric:robolectric:3.6.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
